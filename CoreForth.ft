HEX

LATESTCORE LATEST !  20001B00 FIXUPS !   0 FIXUPS @ !   HERE

DECIMAL

: RELOCATE
    SWAP DUP CORETOP - >R   SWAP OVER -
    FIXUPS @ DUP CELL + SWAP @ 0 DO
        DUP I CELLS + @ DUP @ R@ - SWAP !
    LOOP DROP RDROP
    ROT >R 
    0 DO
        DUP I + DUP R@ = IF CR S"    .set link, ." TYPE CR S"    .byte " TYPE 
        ELSE I 16 MOD 0= IF CR S"    .byte " TYPE ELSE S" , " TYPE THEN THEN
        C@ .U
    LOOP RDROP
    BYE ;

: TIB-TAIL   >TIB @ TIB# @ OVER - SWAP TIB + SWAP ;
: .TIB-TAIL   CLR-EOL !CURSOR TIB-TAIL TYPE @CURSOR ;
: CURSOR>   1 >TIB +! CURSOR-RIGHT ;
: CURSOR<   1 >TIB -! CURSOR-LEFT ;
: INSERT   TIB-TAIL OVER 1+ SWAP CMOVE>   1 TIB# +!   TIB >TIB @ + C! ;
: DELETE   1 TIB# -! TIB-TAIL OVER 1+ -ROT CMOVE ;
: -START?   >TIB @ 0> ;   : -END?   >TIB @ TIB# @ < ;
: -FULL?   TIB# @ #TIB < ;

: ACCEPT
    0 TIB# !   0 >TIB !
    BEGIN
        KEY
        CASE
            DUP 32 127 WITHIN -FULL? AND IF INSERT .TIB-TAIL CURSOR> ELSE
            DUP 127 = -START? AND IF DROP CURSOR< DELETE .TIB-TAIL ELSE
            DUP 8 = OVER -103 = OR -END? AND IF DROP DELETE .TIB-TAIL ELSE
            DUP 13 = OVER 10 = OR IF DROP TIB# @ EXIT ELSE
            DUP -3 = -END? AND IF DROP CURSOR> ELSE
            DUP -4 = -START? AND IF DROP CURSOR< ELSE
        ENDCASE
    AGAIN ;

: INTERPRET
    0 STATE !   TIB (SOURCE) !   0 >SOURCE !
    ACCEPT   SOURCE# !   SPACE
    (INTERPRET) IF DROP S"  ok " TYPE ELSE COUNT TYPE 63 EMIT THEN CR ;

: QUIT
    S0 SP!   R0 RP!
    S" CoreForth ready." TYPE CR
    BEGIN INTERPRET AGAIN ;

HERE  LATEST @ -ROT

FIXUPS @ -ROT   0 FIXUPS !

: XLOCATE
    SWAP DUP CORETOP - >R   SWAP OVER -
    FIXUPS @ DUP CELL + SWAP @ 0 DO
        DUP I CELLS + @ DUP @ R@ - SWAP !
    LOOP DROP RDROP
    ROT >R 
    0 DO
        DUP I + DUP R@ = IF CR S"    .set link, ." TYPE CR S"    .byte " TYPE 
        ELSE I 16 MOD 0= IF CR S"    .byte " TYPE ELSE S" , " TYPE THEN THEN
        C@ .U
    LOOP RDROP
    BYE ;

ROT FIXUPS !

XLOCATE
