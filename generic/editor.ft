DECIMAL

64 CONSTANT C/L
VARIABLE SCR
VARIABLE BLOCK#

: BLOCK   ((BLOCK)) SWAP DUP BLOCK# @ <> IF 2DUP (BLOCK) BLOCK# ! ELSE DROP THEN ;
: UPDATE   ((BLOCK)) BLOCK# @ (UPDATE) ;

: >BLK ( i -- addr )   ((BLOCK)) + ;
: CLR-LAST ( i -- )   C/L 1- OR >BLK BL SWAP C! ;
: >TAIL-LENGTH ( i -- )  C/L TUCK 1- AND - ;
: .TAIL ( i -- )   !CURSOR   DUP >BLK SWAP >TAIL-LENGTH TYPE   @CURSOR ;
: TAIL> ( i -- )   DUP >BLK DUP 1+ ROT >TAIL-LENGTH 1- CMOVE> ;
: TAIL< ( i -- )   DUP DUP >BLK DUP 1+ SWAP ROT >TAIL-LENGTH CMOVE CLR-LAST ;
: LL ( l -- )  124 TUCK EMIT SPACE   C/L * SCR @ BLOCK + C/L TYPE   SPACE EMIT ;
: LIST ( n -- )  DUP SCR !  .D S"  scr" TYPE CR 0 BEGIN DUP LL CR 1+ DUP 16 = UNTIL DROP ;
: RE ( -- n ) SCR @ ;
: L ( -- ) 0 DUP AT-XY RE LIST ;
: B ( -- ) -1 SCR +! ;
: N ( -- ) 1 SCR +! ;
: !XY ( i -- i ) C/BLK 1- AND DUP C/L /MOD 1+ SWAP 2 + SWAP AT-XY ;
: !CH ( c i -- c i )   DUP TAIL>   2DUP >BLK C!  DUP .TAIL ;
: LINE-START  ( i -- start ) C/L 1- INVERT AND >BLK ;
: LINE-END ( i -- end ) LINE-START C/L + 1- ;
: BLANK-LINE ( i -- )  LINE-START C/L 1- BLANK ;
: INS-LINE ( i -- )  DUP LINE-START DUP C/L + DUP C/BLK >BLK SWAP - CMOVE> BLANK-LINE ;

: ?CH ( c i -- c i' )
    OVER BL - 95 U< IF !CH 1+ EXIT THEN ( text )
    OVER KEY-LEFT = IF 1- THEN
    OVER KEY-RIGHT = IF 1+ THEN
    OVER KEY-UP = IF C/L - THEN
    OVER KEY-DOWN = IF C/L + THEN
    OVER KEY-HOME = IF C/L 1- INVERT AND THEN
    OVER KEY-DELETE = IF DUP TAIL< DUP .TAIL THEN
    OVER KEY-BACKSPACE = IF 1- DUP TAIL< !XY DUP .TAIL THEN
    OVER KEY-PGUP = IF UPDATE B L DROP 0 THEN
    OVER KEY-PGDOWN = IF UPDATE N L DROP 0 THEN
    OVER 9 = IF DUP INS-LINE L THEN
    OVER 11 = IF DUP BLANK-LINE L C/L 1- INVERT AND THEN
    OVER 13 = IF C/L 2DUP MOD - + THEN ( crlf return ) ;
: EDIT ( n -- ) CLS 0 DUP AT-XY LIST 0 BEGIN !XY KEY SWAP ?CH SWAP 24 = UNTIL UPDATE DROP L ;
