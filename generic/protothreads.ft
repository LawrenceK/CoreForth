HEX

: PT:                                   \ create protothread word
    CREATE HERE CELL + , ]              \ reserve one cell for the word's IP
    DOES> @ >R ;                        \ fetch IP and jump there via the return stack

: ;PT                                   \ restore IP at word exit
    ['] LIT ,XT   LATEST @ LINK> >BODY DUP CELL + , \ push IP as word's body
    ['] LIT ,XT   ,                                 \ push addr of IP cell
    ['] ! ,XT   ['] ; EXECUTE ; IMMEDIATE           \ set IP and compile normal exit

: ?YIELD                                \ insert yield
    ['] IF EXECUTE                      \ start IF
    ['] LIT ,XT   SWAP ,                    \ push point of BEGIN
    ['] LIT ,XT   LATEST @ LINK> >BODY ,    \ push addr of IP cell
    ['] ! ,XT   ['] EXIT ,XT               \ update IP and exit
    ['] THEN EXECUTE ; IMMEDIATE        \ end IF
