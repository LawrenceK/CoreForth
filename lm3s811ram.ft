HEX

: PLL-50HZ
    SYSCTL-RCC
        DUP DUP @ 0F83FFFFF AND 800 OR SWAP !
        DUP DUP @ 0FFFFCFCF AND SWAP !
        DUP @ 0F87FFC3F AND 01C002C0 OR SWAP !
    SYSCTL-RIS BEGIN DUP @ 40 AND UNTIL DROP ;

: USE-PLL   SYSCTL-RCC DUP @ FFFF7FF AND SWAP ! ;

: UART0-DISABLE   0 UART0 UART-CR ! ;
: UART0-ENABLE   301 UART0 UART-CR ! ;
: UART0-BRR!   UART0 UART-FBRD ! UART0 UART-IBRD ! ;
: UART0-BRR@   UART0 UART-IBRD @ UART0 UART-FBRD @ ;

: LED0-ENABLE   20 GPIOC GPIO-DIR SET-BITS ;
: LED0!   1 = 20 AND 20 GPIOC GPIO-DATA! ;

DECIMAL

: (.S) !CURSOR 0 24 AT-XY .S @CURSOR ;

64 CONSTANT C/L
0 VARIABLE SCR
-1 VARIABLE BLOCK#

: ((BLOCK)) PAD ;
: (BLOCK)   DROP 1024 BLANK ;
: (UPDATE)   2DROP ;
: BLOCK ((BLOCK)) SWAP DUP BLOCK# @ <> IF 2DUP (BLOCK) BLOCK# ! ELSE DROP THEN ;
: UPDATE ((BLOCK)) BLOCK# @ (UPDATE) ;

: >BLK ( i -- addr )   ((BLOCK)) + ;
: CLR-LAST ( i -- )   C/L 1- OR >BLK BL SWAP C! ;
: >TAIL-LENGTH ( i -- )  C/L TUCK 1- AND - ;
: .TAIL ( i -- )   !CURSOR   DUP >BLK SWAP >TAIL-LENGTH TYPE   @CURSOR ;
: TAIL> ( i -- )   DUP >BLK DUP 1+ ROT >TAIL-LENGTH 1- CMOVE> ;
: TAIL< ( i -- )   DUP DUP >BLK DUP 1+ SWAP ROT >TAIL-LENGTH CMOVE CLR-LAST ;
: LL   C/L * SCR @ BLOCK + C/L TYPE ;
: LIST ( n -- )  DUP SCR !  .D S" scr" TYPE CR 0 BEGIN DUP LL CR 1+ DUP 16 = UNTIL DROP ;
: RE ( -- n ) SCR @ ;
: L ( -- ) 0 DUP AT-XY RE LIST ;
: B ( -- ) -1 SCR +! ;
: N ( -- ) 1 SCR +! ;
: !XY ( i -- i ) 1023 AND DUP C/L /MOD 1+ AT-XY ;
: !CH ( c i -- c i )   DUP TAIL>   2DUP >BLK C!  DUP .TAIL ;

: ?CH ( c i -- c i' )
    OVER BL - 95 U< IF !CH 1+ EXIT THEN ( text )
    OVER KEY-LEFT = IF 1- THEN
    OVER KEY-RIGHT = IF 1+ THEN
    OVER KEY-UP = IF C/L - THEN
    OVER KEY-DOWN = IF C/L + THEN
    OVER KEY-HOME = IF C/L 1- INVERT AND THEN
    OVER KEY-DELETE = IF DUP TAIL< DUP .TAIL THEN
    OVER KEY-BACKSPACE = IF 1- DUP TAIL< !XY DUP .TAIL THEN
    OVER KEY-PGUP = IF UPDATE B L DROP 0 THEN
    OVER KEY-PGDOWN = IF UPDATE N L DROP 0 THEN
    OVER 13 = IF C/L 2DUP MOD - + THEN ( crlf return ) ;
: EDIT ( n -- ) CLS 0 DUP AT-XY LIST 0 BEGIN !XY KEY SWAP ?CH SWAP 24 = UNTIL UPDATE DROP L ;

HEX

QUIT
